PROMETHEUS_VERSION := "2.36.1"

# Default command when no verb specified
_default:
  @just --choose

# Build all the project
build: market fog_node

# Build the market
market:
  cargo build --package manager --bin market

# Build the node
fog_node:
  cargo build --package manager --bin fog_node	

# Build the project as a docker image
docker:
  docker build -t fog_node:latest --target fog_node .
  docker build -t market:latest --target market .

# Push an image to ghcr
_push image user:
  docker tag {{image}} ghcr.io/{{user}}/{{image}}
  docker push ghcr.io/{{user}}/{{image}}
  
# Push docker images to ghcr
ghcr user: docker
  just _push fog_node:latest {{user}}
  just _push market:latest {{user}}
  
# Start prometheus locally
prometheus:
  if ! test -d "./prometheus"; then just install-prometheus; fi
  ./prometheus/prometheus --config.file=prometheus.yaml
	
# Install prometheus locally
install-prometheus:
  mkdir -p prometheus
  curl -L https://github.com/prometheus/prometheus/releases/download/v{{PROMETHEUS_VERSION}}/prometheus-{{PROMETHEUS_VERSION}}.linux-amd64.tar.gz | tar -xvz -C prometheus --strip-components=1
  echo "Installed prometheus version {{PROMETHEUS_VERSION}} locally"

# Cleans
clean:
  rm -rf ./prometheus
